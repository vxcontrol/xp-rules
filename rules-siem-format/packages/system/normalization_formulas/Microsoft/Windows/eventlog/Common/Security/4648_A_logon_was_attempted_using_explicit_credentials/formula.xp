EVENTLOG = 'EventID="4648"'

COND = lower($Channel) == "security" and $Provider["Name"] == "Microsoft-Windows-Security-Auditing"

subject = "account"

if maybe_number16($Data["SubjectLogonId"]) <= 1000 then
    action = "login"
    object = "system"

    subject.account.id = $Data["TargetUserSid"]
    subject.account.name = lower($Data["TargetUserName"])
    subject.account.domain = lower($Data["TargetDomainName"])
    subject.account.session_id = maybe_number16($Data["TargetLogonId"])

    category.generic = "Operating System"
    category.high = "Access Management"
    category.low = "Communication"
else
    action = "elevate"
    object = "account"

    subject.account.id = $Data["SubjectUserSid"]
    subject.account.name = lower($Data["SubjectUserName"])
    subject.account.domain = lower($Data["SubjectDomainName"])
    subject.account.session_id = maybe_number16($Data["SubjectLogonId"])

    object.account.id = $Data["TargetUserSid"]
    object.account.name = lower($Data["TargetUserName"])
    object.account.domain = lower($Data["TargetDomainName"])
    object.account.session_id = maybe_number16($Data["TargetLogonId"])

    category.generic = "Account"
    category.high = "Users And Rights Management"
    category.low = "Manipulation"
endif

status = "success"

subject.process.id = maybe_number16($Data["ProcessId"])

$process_fullpath = lower($Data["ProcessName"])
$process_fullpath_csv = csv($process_fullpath, "\\", "")
$process_name = $process_fullpath_csv[length($process_fullpath_csv) - 1]
subject.process.name = $process_name
subject.process.path = strip($process_fullpath, "", $process_name)
subject.process.fullpath = $process_fullpath

datafield6 = strip($Data['LogonGuid'], '{', '}')        
datafield8 = strip($Data['TargetLogonGuid'], '{', '}')  

if (maybe_ipv4($Data["IpAddress"]) != null) || (maybe_ipv6($Data["IpAddress"]) != null) then
    src.ip = $Data["IpAddress"]
endif

if $Data["IpPort"] != "-" then
    src.port = $Data["IpPort"]
endif

if find_substr($Data["TargetServerName"], '.') != null then
    dst.fqdn = lower($Data["TargetServerName"])
    dst.hostname = lower(substr($Data["TargetServerName"], 0, find_substr($Data["TargetServerName"], '.')))
elif find_substr(lower($Data["TargetServerName"]), 'localhost') == null then
    dst.hostname = lower($Data["TargetServerName"])
endif

time = $TimeCreated["SystemTime"]
msgid = $EventID
importance = "medium"

event_src.vendor = "microsoft"
event_src.title = "windows"
event_src.subsys = $Channel

if find_substr($Computer, '.') != null then
    event_src.fqdn = lower($Computer)
    event_src.hostname = lower(substr($Computer, 0, find_substr($Computer, '.')))
else
    event_src.hostname = lower($Computer)
endif

event_src.category = "AAA"

id = "PT_Microsoft_Windows_eventlog_4648_A_logon_was_attempted_using_explicit_credentials"

