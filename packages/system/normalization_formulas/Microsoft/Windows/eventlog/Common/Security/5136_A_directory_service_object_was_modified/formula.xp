EVENTLOG = 'EventID="5136"'
COND = lower($Channel)=="security" and $Provider["Name"]=="Microsoft-Windows-Security-Auditing"

subject = "account"
action = "modify"
object = "ds_object"
status = "success"

subject.account.id = $Data["SubjectUserSid"]
subject.account.domain = lower($Data["SubjectDomainName"])
subject.account.name = lower($Data["SubjectUserName"])
subject.account.session_id = maybe_number16($Data["SubjectLogonId"])

object.name = $Data["ObjectDN"]
object.type = $Data["ObjectClass"]
object.property = $Data["AttributeLDAPDisplayName"]
object.value = $Data["AttributeValue"]
object.state = switch $Data["OperationType"]
    case "%%14674" "value added"
    case "%%14675" "value deleted"
endswitch

subformula "parse_DN"
    TEXT = "cn={$temp_id=UNTIL(',')}{REST}"
    
    object.id = $temp_id + "$"
endsubformula

if find_substr(lower(object.name), "cn") != null then
    submessage("TEXT", "parse_DN", lower(object.name))
endif

chain_id = $Data["OpCorrelationID"]

time = $TimeCreated["SystemTime"]
msgid = $EventID
importance = "low"

category.generic = "Directory Service Object"
category.high = "System Management"
category.low = "Manipulation"

event_src.vendor = "microsoft"
event_src.title = "active_directory"
event_src.subsys = $Channel
if find_substr($Computer, '.') != null then
    event_src.fqdn = lower($Computer)
    event_src.hostname = lower(substr($Computer, 0, find_substr($Computer, '.')))
else
    event_src.hostname = lower($Computer)
endif
event_src.category = "Directory service"

id = "PT_Microsoft_Windows_eventlog_5136_A_directory_service_object_was_modified"

