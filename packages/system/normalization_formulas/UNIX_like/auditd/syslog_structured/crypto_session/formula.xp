JSON = ''
COND = (
    $eventid != null and
    $node != null and
    $timestamp != null and
    $items["CRYPTO_SESSION"] != null
)

$kv_main = keyvalue($items["CRYPTO_SESSION"][0], " ", "=", "'")
$kv_msg = keyvalue($kv_main["msg"], " ", "=", '"')

if $kv_main["auid"] != "4294967295" then
    subject = "account"
endif

action = "open"
object = "session"

if $kv_msg["res"] == "success" then
    status = "success"
else
    status = "failure"
    reason = $kv_msg["op"]
endif

object.type = "encrypted_session"

$property = ""
$value = ""

if ($kv_msg["cipher"] != null) and ($kv_msg["cipher"] != "<implicit>") then
    $property = $property + " | cipher"
    $value = $value + " | " + $kv_msg["cipher"]
endif

if ($kv_msg["ksize"] != null) and ($kv_msg["ksize"] != "<implicit>") then
    $property = $property + " | key_size"
    $value = $value + " | " + $kv_msg["ksize"]
endif

if ($kv_msg["mac"] != null) and ($kv_msg["mac"] != "<implicit>") then
    $property = $property + " | mac_algo"
    $value = $value + " | " + $kv_msg["mac"]
endif

if ($kv_msg["pfs"] != null) and ($kv_msg["pfs"] != "<implicit>") then
    $property = $property + " | pfs"
    $value = $value + " | " + $kv_msg["pfs"]
endif

object.property = if $property != "" then strip($property, " | ", "") else null endif
object.value = if $value != "" then strip($value, " | ", "") else null endif

if not in_list([$kv_msg["addr"], "?"], $kv_msg["hostname"]) then
    src.hostname = $kv_msg["hostname"]
endif

if $kv_msg["addr"] != "?" then
    src.ip = $kv_msg["addr"]
endif

src.port = $kv_msg["rport"]

dst.ip = $kv_msg["laddr"]
dst.port = $kv_msg["lport"]

if $kv_msg["direction"] != "?" then
    direction = $kv_msg["direction"]
endif

chain_id = $eventid  # AuditD Event ID

time = epoch_to_datetime(number($timestamp))
msgid = "CRYPTO_SESSION"
importance = "low"

category.generic = "Session"
category.high = "Network Interaction Management"
category.low = "Communication"

event_src.title = "unix_like"
event_src.subsys = "auditd"
event_src.category = "Operating system"

if $node != "" then
    if maybe_ipv4($node) != null or maybe_ipv6($node) != null then
        if (in_subnet($node, "127.0.0.0/8") == False and $node != "::1") then
            event_src.ip = $node
            labels = "node_contains_ext_ip"
        endif
    else
        $first_dot = find_substr($node, ".")
        if $first_dot != null then
            event_src.fqdn = $node
            event_src.hostname = substr($node, 0, $first_dot)
        else
            event_src.hostname = $node
        endif
    endif
else
    event_src.ip = recv_ipv4
endif

id = "PT_UNIX_like_auditd_syslog_structured_crypto_session"