JSON = ''
COND = (
    $eventid != null and
    $node != null and
    $timestamp != null and
    $items["CRED_ACQ"] != null
)

$kv_main = keyvalue($items["CRED_ACQ"][0], " ", "=", "'")
$kv_msg = keyvalue($kv_main['msg'], ' ', '=', '"')

if $kv_msg["op"] != "PAM:setcred" then  
    drop
endif
    
if $kv_msg["exe"] != "?" then
    $exe = strip($kv_msg["exe"], '"', '"')
    if $exe == $kv_msg["exe"] then
        $exe = hex_to_string($kv_msg["exe"])
    endif
    
    $process_path_and_name = csv($exe, "/", "")
    $process_fullpath = $exe
    $process_name = $process_path_and_name[length($process_path_and_name) - 1]
    $process_path = strip($exe, "", $process_name)
endif

subject = "account"

if ($process_name == "su") then
    action = "elevate"
    object = "account"
else
    action = "login"
    object = "system"
endif

status = if ($kv_msg["res"] == "success") then "ongoing" else "failure" endif

if ($kv_main["ses"] != "4294967295") then
    subject.account.session_id = $kv_main["ses"]
endif

$acct = strip($kv_msg["acct"], '"', '"')
if $acct == $kv_msg["acct"] then
    $acct = hex_to_string($kv_msg["acct"])
endif

$auid_list = csv(strip($kv_main["AUID"], '"', '"'), "@", "")
$uid_list = csv(strip($kv_main["UID"], '"', '"'), "@", "")
$id_list = csv(strip($kv_main["ID"], '"', '"'), "@", "")
$acct_list = csv($acct, "@", "")

if $process_name == "su" then
    subject.account.id = $kv_main["uid"]
    subject.account.name = $uid_list[0]
    subject.account.domain = $uid_list[1]
    
    object.account.name = $acct_list[0]
    object.account.domain = $acct_list[1]

elif in_list(["sshd", "gdm-session-worker", "login"], $process_name) and ($kv_main["ses"] != "4294967295") then
    subject.account.id = $kv_main["auid"]

    if $auid_list != null then
        subject.account.name = $auid_list[0]
        subject.account.domain = $auid_list[1]
    else
        subject.account.name = $acct_list[0]
        subject.account.domain = $acct_list[1]
    endif

else
    if ($kv_msg["id"] != null) then
        subject.account.id = $kv_msg["id"]
        subject.account.name = $id_list[0]
        subject.account.domain = $id_list[1]
    else
        subject.account.name = $acct_list[0]
        subject.account.domain = $acct_list[1]
    endif
endif

if ($kv_msg["res"] == "success") then
    subject.state = "acquired-credentials"
endif

if in_list(["su", "sudo"], $process_name) then
    subject.process.fullpath = $process_fullpath
    subject.process.name = $process_name
    subject.process.path = $process_path
    subject.process.id = $kv_main["pid"]
else
    object.process.fullpath = $process_fullpath
    object.process.name = $process_name
    object.process.path = $process_path
    object.process.id = $kv_main["pid"]
endif

if action == "login" then
    if $process_name == "sshd" then
        logon_auth_method = "remote"
        
        if not in_list([$kv_msg["addr"], "?"], $kv_msg["hostname"]) then
            src.hostname = $kv_msg["hostname"]
        endif

        if ($kv_msg["addr"] != "?") then
            src.ip = $kv_msg["addr"]
        endif
    else
        logon_auth_method = "local"
    endif
endif

if $kv_msg["terminal"] != "?" then
    logon_service = replace(strip($kv_msg["terminal"], "/dev/", ""), "", "/")
endif

chain_id = $eventid  # AuditD Event ID

reason = $kv_msg["op"]

time = epoch_to_datetime(number($timestamp))
msgid = "CRED_ACQ"
importance = "low"

if ($process_name == "su") then
    category.generic = "Account"
    category.high = "Users And Rights Management"
    category.low = "Manipulation"
else
    category.generic = "Operating System"
    category.high = "Access Management"
    category.low = "Communication"
endif

event_src.title = "unix_like"
event_src.subsys = "auditd"
event_src.category = "AAA"

if $node != "" then
    if maybe_ipv4($node) != null or maybe_ipv6($node) != null then
        if (in_subnet($node, "127.0.0.0/8") == False and $node != "::1") then
            event_src.ip = $node
            labels = "node_contains_ext_ip"
            $dst_ip = $node
        endif
    else
        $first_dot = find_substr($node, ".")
        if $first_dot != null then
            event_src.fqdn = $node
            event_src.hostname = substr($node, 0, $first_dot)
            $dst_fqdn = event_src.fqdn
            $dst_hostname = event_src.hostname
        else
            event_src.hostname = $node
            $dst_hostname = event_src.hostname
        endif
    endif
else
    event_src.ip = recv_ipv4
endif

if logon_auth_method == "remote" then
    dst.ip = $dst_ip
    dst.fqdn = $dst_fqdn
    dst.hostname = $dst_hostname
endif

id = "PT_UNIX_like_auditd_syslog_structured_cred_acq"

