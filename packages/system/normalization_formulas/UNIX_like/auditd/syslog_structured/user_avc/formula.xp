JSON = ''
COND = (
    $eventid != null and
    $node != null and
    $timestamp != null and
    $items["USER_AVC"] != null
)

$kv_main = keyvalue($items["USER_AVC"][0], " ", "=", "'")
$kv_msg = keyvalue($kv_main["msg"], " ", "=", '"')

if $kv_msg["exe"] != "?" then
    $exe = strip($kv_msg["exe"], '"', '"')
    if $exe == $kv_msg["exe"] then
        $exe = hex_to_string($kv_msg["exe"])
    endif
    
    $process_path_and_name = csv($exe, "/", "")
    $process_fullpath = $exe
    $process_name = $process_path_and_name[length($process_path_and_name) - 1]
    $process_path = strip($exe, "", $process_name)
endif

# Support for D-Bus and SystemD
$dbus_exe = ($process_name == "dbus-daemon")
$systemd_exe = ($process_name == "systemd")

# Parsing SELinux Messages
subformula "detect_selinux_action"
    TEXT = 'avc: {UNTIL("received")}{$rest=UNTIL(" exe=")} {REST}'
    $modify_conf = true
endsubformula

subformula "detect_selinux_action"
    TEXT = 'avc: {$access=UNTIL("{")}{$operation=UNTIL(" for ")}{$kv_msg_rest=REST}'
    $kv_msg_se = keyvalue($kv_msg_rest, " ", "=", '"')
    $exec_command = true
endsubformula

subformula "parse_enriched"
    TEXT = '{STRING+} UID="{$uid_main=LITERAL*}" AUID="{$auid_main=LITERAL*}" AUID="{$auid_msg=LITERAL*}" UID="{$uid_msg=LITERAL*}" GID="{$gid_msg=LITERAL*}"'
endsubformula

subformula "parse_enriched"
    TEXT = '{REST}'
endsubformula

if $kv_msg["apparmor"] == null then

    submessage("TEXT", "detect_selinux_action", $kv_main["msg"])

    if $modify_conf then
        subject = "process"
        action = "modify"
        object = "configuration"
        status = "success"

        subject.account.id = $kv_main["uid"]
        subject.account.name = strip($kv_main["UID"], '"', '"')

        subject.process.fullpath = $process_fullpath
        subject.process.name = $process_name
        subject.process.path = $process_path
        subject.process.id = $kv_main["pid"]

        reason = "received " + strip($rest, " ", " ")

    elif $exec_command then
        if $dbus_exe then
            subject = "process"
            action = "execute"
            object = "command"

            subject.process.id = $kv_msg_se["spid"]
            subject.privileges = $kv_msg_se["scontext"]
            if ($kv_msg_se["member"] != null) then
                object.value = strip($operation, "", " }") + " " + $kv_msg_se["msgtype"] + "(" + $kv_msg_se["member"] + ")"
            else
                object.value = strip($operation, "", " }") + " " + $kv_msg_se["msgtype"]
            endif

        elif $systemd_exe then
            if $kv_msg_se["auid"] != "n/a" then
                subject = "account"
            else
                subject = "process"
            endif
            action = "execute"
            object = "command"

            if $kv_msg_se["auid"] != "n/a" then
                subject.account.id = $kv_msg_se["auid"]
                
                
                if ($kv_main["UID"] != null) then
                    submessage("TEXT", "parse_enriched", $items["USER_AVC"][0])

                    subject.account.name = $auid_msg
                    object.account.name = $uid_msg
                endif
            endif

            subject.process.id = $kv_main["pid"]
            subject.privileges = $kv_msg_se["scontext"]

            object.account.id = $kv_msg_se["uid"]
            object.account.group = $kv_msg_se["gid"]
            object.value = strip($operation, "", " }")
            if $kv_msg_se["path"] != null then
                 object.value = object.value + " " + $kv_msg_se["path"]
            endif
            object.process.cmdline = $kv_msg_se["cmdline"]
        else
            drop
        endif

        status = if replace($access, "", " ") == "granted" then "success" else "failure" endif

        reason = replace($access, "", " ")
    else
        drop
    endif

else
    if $dbus_exe then
        subject = "process"
        action = "execute"
        object = "command"

        if $kv_msg["apparmor"] == "ALLOWED" then
            status = "success"
        elif $kv_msg["apparmor"] == "DENIED" then
            status = "failure"
        endif

        if $kv_msg["mask"] == "send" or $kv_msg["mask"] == "receive" then
            subject.process.id = $kv_msg["pid"]
            subject.privileges = $kv_msg["label"]
            subject.state = $kv_msg["info"]

            object.state = $kv_msg["peer_info"]
            object.value = $kv_msg["mask"] + " " + $kv_msg["operation"]
            if ($kv_msg["member"] != null) then
                 object.value = object.value + "(" + $kv_msg["member"] + ")"
            endif
        endif
        if $kv_msg["mask"] == "bind" then
            subject.process.id = $kv_msg["pid"]

            object.value = $kv_msg["operation"] + "(" + $kv_msg["name"] + ")"
        endif

        reason = $kv_msg["apparmor"]
    else
        drop
    endif
endif

if $kv_msg["terminal"] != "?" then
    logon_service = replace(strip($kv_msg["terminal"], "/dev/", ""), "", "/")
endif

chain_id = $eventid  # AuditD Event ID

time = epoch_to_datetime(number($timestamp))
msgid = "USER_AVC"
importance = "low"

if action == "execute" then
    category.generic = "Command"
    category.high = "System Management"
    category.low = "Manipulation"
endif
    
event_src.title = "unix_like"
event_src.subsys = "auditd"
event_src.category = "Operating system"

if $node != "" then
    if maybe_ipv4($node) != null or maybe_ipv6($node) != null then
        if (in_subnet($node, "127.0.0.0/8") == False and $node != "::1") then
            event_src.ip = $node
            labels = "node_contains_ext_ip"
        endif
    else
        $first_dot = find_substr($node, ".")
        if $first_dot != null then
            event_src.fqdn = $node
            event_src.hostname = substr($node, 0, $first_dot)
        else
            event_src.hostname = $node
        endif
    endif
else
    event_src.ip = recv_ipv4
endif

if (event_src.hostname == null) and not in_list([$kv_msg["addr"], "?"], $kv_msg["hostname"]) then
    event_src.hostname = $kv_msg["hostname"]
endif

if event_src.ip == null and $kv_msg["addr"] != "?" then
    event_src.ip = $kv_msg["addr"]
endif

id = "PT_UNIX_like_auditd_syslog_structured_user_avc"

