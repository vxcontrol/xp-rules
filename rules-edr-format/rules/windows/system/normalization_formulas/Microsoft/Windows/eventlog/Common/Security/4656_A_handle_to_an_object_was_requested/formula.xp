EVENTLOG = 'EventID="4656"'

COND = lower($Channel)=="security" and $Provider["Name"]=="Microsoft-Windows-Security-Auditing"

subject = "process"
action = "create"
object = "handle"
if $Keywords == "0x8020000000000000" then
    status = "success"
elif $Keywords == "0x8010000000000000" then
    status = "failure"
endif

subject.account.id = $Data["SubjectUserSid"]
subject.account.domain = lower($Data["SubjectDomainName"])
subject.account.name = lower($Data["SubjectUserName"])
subject.account.privileges = $Data["AccessList"]
subject.account.session_id = maybe_number16($Data["SubjectLogonId"])

$temp = csv($Data["ProcessName"], '\\', '')
subject.process.id = maybe_number16($Data["ProcessId"])
subject.process.name = $temp[length($temp) - 1]
subject.process.path = strip($Data["ProcessName"], '', subject.process.name)
subject.process.fullpath = $Data["ProcessName"]

$name_data = csv(strip($Data["ObjectName"], "", "\\"), "\\", "")
object.name = $name_data[length($name_data) - 1]
object.path = strip(strip($Data["ObjectName"], "", "\\"), "", object.name)
object.fullpath = $Data["ObjectName"]
object.id = $Data["HandleId"]
object.type = $Data["ObjectType"]

msgid = $EventID
importance = "info"
time = $TimeCreated["SystemTime"]

event_src.vendor = "microsoft"
event_src.title = "windows"
event_src.subsys = $Channel
if find_substr($Computer, '.') != null then
    event_src.fqdn = lower($Computer)
    event_src.hostname = lower(substr($Computer, 0, find_substr($Computer, '.')))
else
    event_src.hostname = lower($Computer)
endif
event_src.category = "File service"

id = "PT_Microsoft_Windows_eventlog_4656_A_handle_to_an_object_was_requested"

