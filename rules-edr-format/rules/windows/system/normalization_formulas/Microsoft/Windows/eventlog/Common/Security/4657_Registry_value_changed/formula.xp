EVENTLOG = 'EventID="4657"'
COND = lower($Channel)=="security" and $Provider["Name"]=="Microsoft-Windows-Security-Auditing"

subject = "account"
action = switch $Data["OperationType"]
    case "%%1904" "create"
    case "%%1905" "modify"
    case "%%1906" "remove"
endswitch
object = "reg_object"

if $Keywords == "0x8020000000000000" then
    status = "success"
elif $Keywords == "0x8010000000000000" then
    status = "failure"
endif

subject.account.id = $Data["SubjectUserSid"]
subject.account.domain = lower($Data["SubjectDomainName"])
subject.account.name = lower($Data["SubjectUserName"])
subject.account.session_id = maybe_number16($Data["SubjectLogonId"])

$temp = csv($Data["ProcessName"], '\\', '')
subject.process.id = maybe_number16($Data["ProcessID"])
subject.process.name = $temp[length($temp)-1]
subject.process.path = strip($Data["ProcessName"], '', subject.process.name)
subject.process.fullpath = $Data["ProcessName"]

object.path = lower($Data["ObjectName"])
object.name = lower($Data["ObjectValueName"])
object.property = "value"
object.value = lower($Data["OldValue"])
object.new_value = lower($Data["NewValue"])
object.fullpath = object.path + "\\" + object.name

time = $TimeCreated["SystemTime"]
msgid = $EventID
importance = "info"

category.generic = "Registry Object"
category.high = "System Management"
category.low = "Manipulation"

event_src.subsys = $Channel
if find_substr($Computer, '.') != null then
    event_src.fqdn = lower($Computer)
    event_src.hostname = lower(substr($Computer, 0, find_substr($Computer, '.')))
else
    event_src.hostname = lower($Computer)
endif
event_src.vendor = "microsoft"
event_src.title = "windows"
event_src.category = "Operating system"

id = "PT_Microsoft_Windows_eventlog_4657_Registry_value_changed"

