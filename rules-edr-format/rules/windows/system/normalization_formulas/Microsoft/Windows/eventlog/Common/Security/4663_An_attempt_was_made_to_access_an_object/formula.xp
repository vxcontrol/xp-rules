EVENTLOG = 'EventID="4663"'

COND = lower($Channel)=="security" and $Provider["Name"]=="Microsoft-Windows-Security-Auditing"

subject = "account"
action = "access"
status = "success"

if $Data["ObjectType"] == "File" then
    object = "file_object"
    
    $tmp_list = csv($Data["ObjectName"], "\\", "")
    object.name = lower($tmp_list[length($tmp_list) - 1])
    object.path = strip(lower($Data["ObjectName"]), "", object.name)
    
    category.generic = "File System Object"
elif $Data["ObjectType"] == "Key" then
    object = "reg_object"
    
    object.path = lower($Data["ObjectName"])

    category.generic = "Registry Object"
else
    object = "resource"
    
    $tmp_list = csv($Data["ObjectName"], "\\", "")
    object.name = lower($tmp_list[length($tmp_list) - 1])
    object.path = strip(lower($Data["ObjectName"]), "", object.name)
    
    category.generic = "Unknown Entity"
endif

subject.account.id = $Data["SubjectUserSid"]
subject.account.domain = lower($Data["SubjectDomainName"])
subject.account.name = lower($Data["SubjectUserName"])
subject.account.privileges = $Data["AccessList"]
subject.account.session_id = maybe_number16($Data["SubjectLogonId"])

$temp = csv($Data["ProcessName"], '\\', '')
subject.process.id = maybe_number16($Data["ProcessId"])
subject.process.name = $temp[length($temp) - 1]
subject.process.path = strip($Data["ProcessName"], '', subject.process.name)
subject.process.fullpath = $Data["ProcessName"]

object.type = lower($Data["ObjectType"])
object.fullpath = object.path + object.name

datafield1 = $Data["HandleId"]                         # handle id
datafield5 = $Data["AccessMask"]                       

time = $TimeCreated["SystemTime"]
msgid = $EventID
importance = "info"

category.high = "System Management"
category.low = "Manipulation"

event_src.vendor = "microsoft"
event_src.title = "windows"
event_src.subsys = $Channel
if find_substr($Computer, '.') != null then
    event_src.fqdn = lower($Computer)
    event_src.hostname = lower(substr($Computer, 0, find_substr($Computer, '.')))
else
    event_src.hostname = lower($Computer)
endif
event_src.category = "Operating system"

id = "PT_Microsoft_Windows_eventlog_4663_An_attempt_was_made_to_access_an_object"

