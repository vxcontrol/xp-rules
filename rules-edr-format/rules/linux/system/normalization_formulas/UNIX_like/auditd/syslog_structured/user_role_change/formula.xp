JSON = ''
COND = (
    $eventid != null and
    $node != null and
    $timestamp != null and
    $items["USER_ROLE_CHANGE"] != null
)

$kv_main = keyvalue($items["USER_ROLE_CHANGE"][0], " ", "=", "'")

subject = "account"
action = "modify"
object = "session"

subformula "parse"
    TEXT = '{$what=UNTIL(": ")} {$kv_main_msg=REST}'
endsubformula

submessage("TEXT", "parse", $kv_main["msg"])

$kv_msg = keyvalue($kv_main_msg, ' ', '=', '"')

status = if ($kv_msg["res"] == "success") then "success" else "failure" endif

$auid_list = csv(strip($kv_main["AUID"], '"', '"'), "@", "")

subject.account.id = $kv_main["auid"]
subject.account.name = $auid_list[0]
subject.account.domain = $auid_list[1]
subject.account.privileges = $kv_main["uid"]

if ($kv_main["auid"] != "4294967295") then
    subject.account.session_id = $kv_main["ses"]
endif

if $kv_msg["exe"] != "?" then
    $exe = strip($kv_msg["exe"], '"', '"')
    if $exe == $kv_msg["exe"] then
        $exe = hex_to_string($kv_msg["exe"])
    endif
    
    $process_path_and_name = csv($exe, "/", "")
    $process_fullpath = $exe
    $process_name = $process_path_and_name[length($process_path_and_name) - 1]
    $process_path = strip($exe, "", $process_name)
endif

subject.process.fullpath = $process_fullpath
subject.process.name = $process_name
subject.process.path = $process_path
subject.process.id = $kv_main["pid"]

object.type = "terminal_session"

if $what == "pam" then
    if ($kv_msg["selected-context"] != "?") then
        object.property = "selinux_context"
        object.value = $kv_msg["default-context"]
        object.new_value = $kv_msg["selected-context"]
    endif
endif

if $what == "newrole" then
    if ($kv_msg["new-context"] != "?") then
        object.property = "selinux_context"
        object.value = $kv_msg["old-context"]
        object.new_value = $kv_msg["new-context"]
    endif
endif

if $kv_msg["terminal"] != "?" then
    logon_service = replace(strip($kv_msg["terminal"], "/dev/", ""), "", "/")
endif

chain_id = $eventid  # AuditD Event ID

time = epoch_to_datetime(number($timestamp))
msgid = "USER_ROLE_CHANGE"
importance = "low"

event_src.title = "unix_like"
event_src.subsys = "auditd"
event_src.category = "Operating system"

if $node != "" then
    if maybe_ipv4($node) != null or maybe_ipv6($node) != null then
        if (in_subnet($node, "127.0.0.0/8") == False and $node != "::1") then
            event_src.ip = $node
            labels = "node_contains_ext_ip"
        endif
    else
        $first_dot = find_substr($node, ".")
        if $first_dot != null then
            event_src.fqdn = $node
            event_src.hostname = substr($node, 0, $first_dot)
        else
            event_src.hostname = $node
        endif
    endif
else
    event_src.ip = recv_ipv4
endif

id = "PT_UNIX_like_auditd_syslog_structured_user_role_change"

