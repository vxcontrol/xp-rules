JSON = ''
COND = (
    $eventid != null and
    $node != null and
    $timestamp != null and
    $items["CRYPTO_KEY_USER"] != null
)

$kv_main = keyvalue($items["CRYPTO_KEY_USER"][0], " ", "=", "'")
$kv_msg = keyvalue($kv_main["msg"], " ", "=", '"')

if $kv_main["auid"] != "4294967295" then
    subject = "account"
endif

if ($kv_msg["op"] == "destroy") then
    action = "remove"
elif ($kv_msg["op"] == "create") then
    action = "create"
else
    drop
endif

object = "resource"
status = if $kv_msg["res"] == "success" then "success" else "failure" endif

$auid_list = csv(strip($kv_main["AUID"], '"', '"'), "@", "")
$uid_list = csv(strip($kv_main["UID"], '"', '"'), "@", "")

if $kv_main["auid"] != "4294967295" then
    subject.account.name = $auid_list[0]
    subject.account.domain = $auid_list[1]

    subject.account.id = $kv_main["auid"]
else
    subject.account.name = $uid_list[0]
    subject.account.domain = $uid_list[1]

    subject.account.id = $kv_main["uid"]
endif

subject.account.session_id = $kv_main["ses"]
subject.account.privileges = $kv_main["uid"]

if $kv_msg["exe"] != "?" then
    $exe = strip($kv_msg["exe"], '"', '"')
    if $exe == $kv_msg["exe"] then
        $exe = hex_to_string($kv_msg["exe"])
    endif
    
    $process_path_and_name = csv($exe, "/", "")
    $process_fullpath = $exe
    $process_name = $process_path_and_name[length($process_path_and_name) - 1]
    $process_path = strip($exe, "", $process_name)
endif

subject.process.fullpath = $process_fullpath
subject.process.name = $process_name
subject.process.path = $process_path
subject.process.id = $kv_main["pid"]

if ($kv_msg["kind"] == "server") then
    object.type = "host_key"
    object.property = "fp"
    object.value = $kv_msg["fp"]
endif

if ($kv_msg["kind"] == "session") then
    object.type = "session_key"
endif

if $kv_msg["kind"] == "session" then
    if not in_list([$kv_msg["addr"], "?"], $kv_msg["hostname"]) then
        src.hostname = $kv_msg["hostname"]
    endif
    
    if $kv_msg["addr"] != "?" then
        src.ip = $kv_msg["addr"]
    endif
    
    src.port = $kv_msg["rport"]
endif

if $kv_msg["kind"] == "session" then
    dst.ip = $kv_msg["laddr"]
    dst.port = $kv_msg["lport"]
endif

if $kv_msg["direction"] != "?" then
    direction = $kv_msg["direction"]
endif

chain_id = $eventid  # AuditD Event ID

time = epoch_to_datetime(number($timestamp))
msgid = "CRYPTO_KEY_USER"
importance = "low"

event_src.title = "unix_like"
event_src.subsys = "auditd"
event_src.category = "Operating system"

if $node != "" then
    if maybe_ipv4($node) != null or maybe_ipv6($node) != null then
        if (in_subnet($node, "127.0.0.0/8") == False and $node != "::1") then
            event_src.ip = $node
            labels = "node_contains_ext_ip"
        endif
    else
        $first_dot = find_substr($node, ".")
        if $first_dot != null then
            event_src.fqdn = $node
            event_src.hostname = substr($node, 0, $first_dot)
        else
            event_src.hostname = $node
        endif
    endif
else
    event_src.ip = recv_ipv4
endif

if (event_src.hostname == null) and not in_list([$kv_msg["addr"], "?"], $kv_msg["hostname"]) then
    event_src.hostname = $kv_msg["hostname"]
endif

if event_src.ip == null and $kv_msg["addr"] != "?" then
    event_src.ip = $kv_msg["addr"]
endif

id = "PT_UNIX_like_auditd_syslog_structured_crypto_key_user"

